plugins {
    id 'idea'
    id "org.jetbrains.kotlin.jvm" version "1.4.30"
    id "org.springframework.boot" version "2.4.3" apply false
    id "io.spring.dependency-management" version "1.0.11.RELEASE" apply false
    id "org.jetbrains.kotlin.plugin.spring" version "1.4.30"
}

ext {
    archive_file_name = "integracao-continua-api.war"
}

repositories {
    mavenCentral()
}

allprojects {
    group = 'com.github.allanfvc'
    version = '1.0.0'
}


idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}


subprojects {
    group = 'com.github.allanfvc'
    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    ext {
        spring_version = "5.3.4"
        springboot_version = "2.4.3"
    }


    dependencies {
        compile "org.jetbrains.kotlin:kotlin-reflect:1.4.30"
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        implementation 'com.javadocmd:simplelatlng:1.3.1'
    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions {
            useIR = true
            jvmTarget = "11"
        }
    }

}

project(':integracao-continua-dataprovider') {

    dependencies {
        api project(':integracao-continua-core')
        implementation 'org.liquibase:liquibase-core:4.3.1'
        implementation 'com.google.code.gson:gson:2.8.6'
        compile "org.springframework.boot:spring-boot-starter-data-jpa:${project.ext.springboot_version}"
    }
}

project(':integracao-continua-entrypoint') {

    dependencies {
        api project(':integracao-continua-dataprovider')
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        compile "org.springframework.boot:spring-boot-starter-web:${project.ext.springboot_version}"
    }
}

project(':integracao-continua-configuration') {

    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.springframework.boot"
    apply plugin: "kotlin-spring"

    dependencies {
        api project(':integracao-continua-entrypoint')
        implementation 'org.postgresql:postgresql:42.2.19'
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        compile "org.springframework.boot:spring-boot-starter-data-redis"
        compile "org.springframework.boot:spring-boot-starter"
        compile "org.jetbrains.kotlin:kotlin-reflect"
        compile "org.jetbrains.kotlin:kotlin-stdlib"
    }

    springBoot {
        mainClass = 'com.github.allanfvc.Application'
    }

}

